dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(etherape, 0.6.9)
AM_CONFIG_HEADER(config.h)

dnl Pick up the Gnome macros.
AM_ACLOCAL_INCLUDE(macros)



GNOME_INIT
AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AM_PROG_LIBTOOL
AC_HEADER_STDC

dnl ******************************
dnl LibGlade checking
dnl ******************************
AC_MSG_CHECKING(for Glade libraries >= 0.11)
if gnome-config --libs libglade > /dev/null 2>&1; then 
    vers=`gnome-config --modversion libglade | awk 'BEGIN { FS = "."; } { print $1 * 1000 + $2;}'`
    if test "$vers" -ge 11; then
        AC_MSG_RESULT(found)
    else
        AC_MSG_ERROR(You need at least libglade 0.11 for this version of Gnumeric)
    fi
else
    AC_MSG_ERROR(Did not find libGlade installed)
fi


GNOME_COMPILE_WARNINGS
GNOME_X_CHECKS

dnl Add the languages which your application supports here.
ALL_LINGUAS="es fr nl"
AM_GNU_GETTEXT

dnl Set PACKAGE_LOCALE_DIR in config.h.
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale")
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale")
fi

dnl Subst PACKAGE_PIXMAPS_DIR.
PACKAGE_PIXMAPS_DIR="`gnome-config --datadir`/pixmaps/${PACKAGE}"
AC_SUBST(PACKAGE_PIXMAPS_DIR)

dnl ANSI C (and POSIX.1)'s math.h guarantees:
dnl acos, asin, atan2, atan, ceil, cos, cosh, exp, fabs, floor, fmod, frexp,
dnl ldexp, log10, log, modf, pow, sin, sinh, sqrt, tan, tanh


dnl Checks for "gethostbyname()" - and "-lnsl", if we need it to get
dnl "gethostbyname()".
AC_ETHEREAL_GETHOSTBY_LIB_CHECK

dnl Checks for "connect()", used as a proxy for "socket()" - and
dnl "-lsocket", if we need it to get "connect()".
AC_ETHEREAL_SOCKET_LIB_CHECK

# Check for the pcap library.

AC_ARG_ENABLE(static-pcap,
  [  --enable-static-pcap    Used to build the rpm within Debian. ],
  STATIC_PCAP=yes)
AC_ETHEREAL_PCAP_CHECK

# Create DATAFILE_DIR #define for config.h
DATAFILE_DIR=$sysconfdir
# The following statement was in ethereal
# but it breaks in some machines and I still don't know
# what is for... JTC :-)
#DATAFILE_DIR=(
#    test "x$prefix" = xNONE && prefix=$ac_default_prefix
#    test "x$exec_prefix" = xNONE && exec_prefix=${prefix}
#    eval echo "$DATAFILE_DIR"
#    )

AC_DEFINE_UNQUOTED(DATAFILE_DIR,"$DATAFILE_DIR")
AC_SUBST(DATAFILE_DIR)

dnl Set PIXMAPS_DIR in config.h.
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PIXMAPS_DIR, "${ac_default_prefix}/${DATADIRNAME}/pixmaps")
else
  AC_DEFINE_UNQUOTED(PIXMAPS_DIR, "${prefix}/${DATADIRNAME}/pixmaps")
fi

# Copied from mtr to support dns.c
# After some kind of change in libc, it seems it is
# required to #include <resolv.h> for the test to succeed.
# To go around it I included the test for __res_init

AC_CHECK_FUNC(res_init, , 
  AC_CHECK_LIB(bind, res_init, , 
   AC_CHECK_LIB(resolv, res_init, ,
    AC_CHECK_LIB(c, __res_init, , AC_MSG_ERROR(No resolver library found)))))

AC_CHECK_FUNC(res_mkquery, , 
  AC_CHECK_LIB(bind, res_mkquery, , 
   AC_CHECK_LIB(resolv, res_mkquery, , AC_MSG_ERROR(No resolver library found))))


dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h sys/ioctl.h sys/time.h unistd.h stdarg.h netdb.h dirent.h)
AC_CHECK_HEADERS(sys/stat.h sys/sockio.h sys/types.h sys/socket.h)
AC_CHECK_HEADERS(sys/wait.h sys/param.h)
AC_CHECK_HEADERS(netinet/in.h net/if.h)
AC_CHECK_HEADERS(stddef.h)
AC_CHECK_HEADERS(dlfcn.h)
AC_CHECK_HEADERS(arpa/inet.h arpa/nameser.h)

AC_OUTPUT([
Makefile
etherape.spec
macros/Makefile
src/Makefile
doc/Makefile
doc/C/Makefile
glade/Makefile
intl/Makefile
po/Makefile.in
])

